node light_source ( sw, c1, c2: bool ) returns ( light_source_status : int )
	let
		automaton
			state Off do
				light_source_status = 0;
				unless (c1 & sw) or not c2 then On

			state On do
				light_source_status = 1;
				unless (c1 & sw) or not c2 then Off
		end
	tel

node door ( c : bool ) returns ( door_status : int )
	let
	    automaton
		    state Closed do
				door_status = 0;
			    unless not c then Open
		    state Open do
				door_status = 1;
			    unless not c then Closed
	    end
	tel

node closet ( c : bool ) returns ( closet_status : int )
	let
		automaton
			state Locked do
				closet_status = 0;
				unless not c then Open
			state Open do
				closet_status = 1;
				unless c then Locked
		end
	tel

node computer ( c : bool ) returns ( computer_on : int )
	let
		automaton
			state Off do
				computer_on = 0;
				unless c then On
			state On do
				computer_on = 1;
				unless not c then Off
		end
	tel

node window ( c : bool ) returns ( window_status : int )
	let
		automaton
			state Closed do
				window_status = 0;
				unless not c then Open
				
			state Open do
				window_status = 1;
				unless c then Closed
				
		end
	tel

node air_conditioner ( c1, c2 : bool ) returns ( air_status : int )
	let
		automaton
			state Off do
				air_status = 0;
				unless c1 then Min
					| not c2 then Max

			state Min do
				air_status = 1;
				unless not c1 then Off
					| not c2 then Max
			
			state Max do
				air_status = 2;
				unless not c1 then Min
					| not c2 then Off
		end
	tel

node person ( arrived : bool ) returns ( presence : bool )
	let
		automaton
			state Not_Present do
				presence = false;
				unless arrived then Present
			state Present do
				presence = true;
				unless arrived then Not_Present
		end
	tel

node day_shift (change : bool) returns ( night : bool)
	let
		automaton
			state Dia do
				night = false
				unless change then Noite
			state Noite do
				night = true
				unless change then Dia
		end
	tel

node controller( change_shift, worker, cleaner, light_switch, blind_switch: bool)
	
	returns (
		night, worker_presence, cleaner_presence : bool; 
		door_status, pc_status, window_status, closet_status, air_status, light_status, blind_status : int;)
  	
  	contract
		var rule0, rule1, rule1_3, rule2, rule1_1, rule1_2, rule5, rule6, rule6_1, rule6_2, rule6_3, rule6_4, rule7, rule7_5, rule8, rule8_5 : bool;

		let
			rule0 = not (not worker_presence & not cleaner_presence) or (air_status <> 1 & air_status <> 2 & light_status <> 1 & blind_status <> 1 & door_status <> 1 & pc_status <> 1 & window_status <> 1 & closet_status <> 1);
			
			rule1 = not ((worker_presence or cleaner_presence) & night & light_status <= 1) or (light_status = 1);
			rule1_3 = not ((worker_presence or cleaner_presence) & night & light_status >= 2 & blind_status <= 1) or blind_status = 1;
			
			rule2 = not ((worker_presence or cleaner_presence) & not night & light_status <= 1 & blind_status <= 1) or (light_status = 1 or blind_status = 1);
			
			rule1_1 = not ((worker_presence or cleaner_presence) & light_status >= 2 & blind_status <= 1) or (blind_status = 1);
			rule1_2 = not ((worker_presence or cleaner_presence) & light_status <= 1 & blind_status >= 2) or (light_status = 1);

			rule5 = not (worker_presence & cleaner_presence & air_status <= 2) or air_status = 2;
			rule6 = not (not (worker_presence & cleaner_presence) & (worker_presence or cleaner_presence) & air_status <= 2) or (air_status = 1 & door_status <> 1 & window_status <> 1);
			
			rule6_1 = not ((worker_presence or cleaner_presence) & air_status >= 3 & blind_status <= 1 & window_status <= 1 & door_status <= 1) or (blind_status = 1 & window_status = 1 & door_status = 1);
			rule6_2 = not ((worker_presence or cleaner_presence) & air_status >= 3 & blind_status >= 2 &                      door_status <= 1) or (window_status <> 1 & door_status = 1);
			rule6_3 = not ((worker_presence or cleaner_presence) & air_status >= 3 & 					 window_status >= 2 & door_status <= 1) or (door_status = 1);
			rule6_4 = not ((worker_presence or cleaner_presence) & air_status >= 3 & blind_status <= 1 & window_status <= 1 & door_status >= 2) or (blind_status = 1 & window_status = 1 &  door_status <> 1);

			rule7 = not (worker_presence & pc_status <= 1) or pc_status = 1;
			rule7_5 = not (not worker_presence & pc_status <= 1) or pc_status = 0;

			rule8 = not (cleaner_presence & closet_status <= 1) or closet_status = 1;
			rule8_5 = not (not cleaner_presence & closet_status <= 1) or closet_status = 0;
		tel
    enforce (rule0 & rule1 & rule1_3 & rule2 & rule1_1 & rule1_2 & rule5 & rule6 & rule6_1 & rule6_2 & rule6_3 & rule6_4 & rule7 & rule7_5 & rule8 & rule8_5)
    with ( c_door, c_pc, c_window, c_air_1, c_air_2, c_closet,  c_light_1, c_blind_1, c_light_2, c_blind_2 : bool )

  	let
  		night = inlined day_shift (change_shift);
		worker_presence = inlined person (worker);
		cleaner_presence = inlined person (cleaner);
		pc_status = inlined computer (c_pc);
		door_status = inlined door (c_door);
		closet_status = inlined closet (c_closet);
		window_status = inlined window (c_window);
		air_status = inlined air_conditioner (c_air_1, c_air_2);
  		light_status = inlined light_source(light_switch, c_light_1, c_light_2);
  		blind_status = inlined light_source(blind_switch, c_blind_1, c_blind_2);
 
	tel
